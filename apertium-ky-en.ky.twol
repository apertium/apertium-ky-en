Alphabet
А Б В Г Д Е Ё Ж З И И К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ь Ы Ъ Э Ю Я
а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ь ы ъ э ю я
%{G%}:г %{D%}:д %{N%}:н %{L%}:л %{B%}:б %{P%}:п 
%{A%}:а %{I%}:ы %{U%}:у %{E%}:а 
%>:0 ; 

Sets

!%{G%}       ! Realised as 'г', 'к', 'ң', 'н'
!%{D%}       ! Realised as 'д', 'т', 'н'
!%{M%}       ! Realised as 'м', 'б', 'п'
!%{N%}       ! Realised as 'н', 'д', 'т'
!%{L%}       ! Realised as 'л', 'д', 'т'

Cns = Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ь Ъ 
	  б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ь ъ 
	  %{D%} %{G%} %{N%} %{L%} %{B%} %{P%} ;

!%{E%}       ! Realised as 'а', 'е', 'о', 'ө', 'й' 
!%{A%}       ! Realised as 'а', 'е', 'о', 'ө' 
!%{I%}       ! Realised as 'ы', 'и', 'у', 'ү'
!%{U%}       ! Realised as 'у', 'ү'

Vow =     А Е Ё И О Ө У Ү Ы Э Ю Я
          а е ё и о ө у ү ы э ю я 
          %{A%} %{I%} %{U%} %{E%} ;

SurVow = А Е Ё И О Ө У Ү Ы Э Ю Я
         а е ё и о ө у ү ы э ю я ; 

FrontVow = И Ө Е Ү Э и ө ү э е ;
BackVow = А О У Ы а о у ы Я я Ю ю ;
FrontUnrndVow = И Е Э и э е ; 
BackUnrndVow = А Ы а ы Я я ;
FrontRndVow = Ө Ү ө ү ;
BackRndVow =  О У о у Ю ю ;
BackLowRndVow = О о Ё ё ;
UyanCons = м ң М Ң ;

SonCns = й р Й Р ;
UnvoicedNonSonCns = К П С Т Ф Х Ц Ч Ш Щ  
	                к п с т ф х ц ч ш щ ;

VoicedNonSonCns = Б В Г Д Ж З Л М Н Ң Й Р 
                  б в г д ж з л м н ң й р ;

VoicedNonSonCnsNoRNoIj = Б В Г Д Ж З Л М Н Ң  
                  б в г д ж з л м н ң ;

! These three sets include the suffix border, and accounts for long-distance VH
CnsFrontUnrndVowSB = Cns FrontUnrndVow %> ;
CnsFrontRndVowSB = Cns FrontRndVow %> ;
CnsBackRndVowSB = Cns BackRndVow %> ;

! These sets do not continue suffix borders.
CnsFrontUnrndVow = Cns FrontUnrndVow %{I%} %{A%} %{U%} %{E%} ;
CnsFrontRndVow = Cns FrontRndVow  %{I%} %{A%} %{U%} %{E%} ;
CnsBackRndVow = Cns BackRndVow  %{I%} %{A%} %{U%} %{E%} ;
CnsBackLowRndVow = Cns BackLowRndVow  %{I%} %{A%} %{U%} %{E%} ;

NotVowel = Cns %> ; 

	   
Rules

!"Suffix vowel deletion in vowel stems"
!Vx:0 <=> SurVow: %>: _ ;
!        where Vx in (  и  ү  ы  у  ) 
!        matched ; 

!"Suffix vowel deletion in vowel stems"
!%{I%}:0 <=> SurVow: %>: _ NotVowel ; 

!0:%{I%} <=> Cns: %>: _ NotVowel: .#. ; 

"Suffix vowel {I} insertion in consonant stems"
0:Vy <=> :LastVowel NotVowel:* Cns: %>: _ NotVowel: .#. ; 
      where Vy in        (   и     ү     и     и     ү     ы     ы     у     у     ы     у     у   )
            LastVowel in (   и     ү     е     э     ө     я     а     ё     о     ы     ю     у   )
       matched;

"Vowel harmony for archiphoneme {A}"
Vx:Vy <=> :LastVowel NotVowel:* _ ; 
        where Vx         in ( %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} %{A%} )
              Vy         in (   а     а     е     е     е     а     а     ө     ө     о     о     ө   )
              LastVowel  in (   я     а     е     э     и     ы     ю     у     ө     ё     о     ү   )
              matched ; 

"Vowel harmony for archiphoneme {I}"
Vx:Vy <=> :LastVowel NotVowel:* _ ; 
        where Vx         in ( %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} %{I%} )
              Vy         in (   и     ү     и     и     ү     ы     ы     у     у     ы     у     у   )
              LastVowel  in (   и     ү     е     э     ө     я     а     ё     о     ы     ю     у   )
              matched ; 

"Vowel harmony for archiphoneme {U}"
Vx:Vy <=> :LastVowel NotVowel:* _ ; 
        where Vx         in ( %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} %{U%} )
              Vy         in (   ү     ү     ү     ү     ү     у     у     у     у     у     у     у   )
              LastVowel  in (   и     ү     е     э     ө     я     а     ё     о     ы     ю     у   )
              matched ; 

"Vowel harmony for archiphoneme {E}"
Vx:Vy <=> :LastVowel NotVowel:* _ ; 
        where Vx         in ( %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} %{E%} )
              Vy         in (   а     а     е     е     е     а     а     ө     ө     о     о     ө   )
              LastVowel  in (   я     а     е     э     и     ы     ю     у     ө     ё     о     ү   )
              matched ; 

"Voiceless non-coronal stop voicing"
Cx:Cy <=> Vow _ %>: Vow ; 
        where Cx in (  п   к  )
              Cy in (  б   г  )
              matched ;

"Voiceless non-coronal stop voicing before possessive"
Cx:Cy <=> Vow _ %>: с: %{I%}: ;
        where Cx in (  п   к  )
              Cy in (  б   г  )
              matched ;

"{D} -> н after {I} and before {A} in ablative" 
%{D%}:н <=> %{I%}: %>: _ %{A%}: н ;

"Мягкий знак deletion before suffix"
ь:0 <=> _ %>: ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!"Realisation of converb marker after vowels"
!%{E%}:й <=> SurVow %>: _ ;

!"Harmonise stem ending before infinitive"
!Vx:Vy <=> Cns _ %>: %{U%}: %{U%}: ; 
!       where Vx in (  е   а   и   ы  ) 
!             Vy in (  ө   о   ү   у  )
!             matched;
!
!"Harmonise {U} in infinitives"
!%{U%}:Vx <=> Vy: %>: _ %{U%}: ; 
!       where Vx in (  ө   о   ү   у  ) 
!             Vy in (  ө   о   ү   у  )
!             matched;
!
!"{U} deletion in vowel final stem"
!%{U%}:0 <=> Vow %>: _ %{U%}: ; 




!
!"Front Unrnd harmony in suffixes"
!Vx:Vy <=> :FrontUnrndVow CnsFrontUnrndVowSB:* Cns %>:     CnsFrontUnrndVow:* _ ; !  ?C>?_
!          :FrontUnrndVow CnsFrontUnrndVowSB:*     %>: Cns CnsFrontUnrndVow:* _ ; !   ?>C_
!	where Vx in ( %{A%} %{I%} %{U%} %{E%} )
!	      Vy in (   е     и     ү     е   )
!	      matched ;
!
!"Front Rnd harmony in suffixes"
!Vx:Vy <=> :FrontRndVow CnsFrontRndVowSB:* Cns %>:     CnsFrontRndVow:* _ ; ! ?C>?_
!          :FrontRndVow CnsFrontRndVowSB:*     %>: Cns CnsFrontRndVow:* _ ; !  ?>C_
!	where Vx in ( %{A%} %{I%} %{U%} %{E%} )
!	      Vy in (   ө     ү     ү     ө   )
!	      matched ;
!
!"Back Low Rnd harmony in suffixes"
!Vx:Vy <=> :BackLowRndVow CnsBackLowRndVow:* Cns %>:     CnsBackLowRndVow:* _ ; ! ?C>?_
!          :BackLowRndVow CnsBackLowRndVow:*     %>: Cns CnsBackLowRndVow:* _ ; !  ?>C_
!        where Vx in ( %{A%} %{I%} %{U%} %{E%} ) 
!              Vy in (   о     у     у     о   )
!              matched ; 
!
!"Back High Rnd harmony in suffixes"
!Vx:Vy <=> :BackRndVow CnsBackRndVowSB:* Cns %>:     CnsBackRndVow:* _ ; ! ?C>?_
!            :BackRndVow CnsBackRndVowSB:*     %>: Cns CnsBackRndVow:* _ ; !  ?>C_
!        where Vx in (       %{I%} %{U%} %{E%} )
!              Vy in (         у     у     о   )
!              matched ; 
!

"L Desonorisation"
%{L%}:д <=> VoicedNonSonCnsNoRNoIj %>:  _ ;

"N Desonorisation"
%{N%}:д <=> [ SonCns | VoicedNonSonCns ] %>:  _ ;

"L Desonorisation and devoicing"
Cx:т <=> [ UnvoicedNonSonCns | UnvoicedNonSonCns ь ] %>: _ ;
	where Cx in ( %{L%} %{N%} ) ;

"Katkalan harmony in consonants"
Cx:Cy <=>  UnvoicedNonSonCns %>: _ ;
        where Cx in ( %{G%}  %{D%}  )
              Cy in (   к      т    )
              matched;

!"Katkalan harmony in consonants 2"
!%{G%}:к <=> UnvoicedNonSonCns %>: _ ;
!
!"Katkalan harmony in consonants 1"
!%{D%}:т <=> UnvoicedNonSonCns %>: _ ;

"'с' deletion in possessive after consonant"
с:0 <=> Cns %>: _ %{I%}: ; 

"'г' deletion after uyan consonants"
%{G%}:0 <=> UyanCons: %>: _ %{A%}: ;

"'г' desonorisation after I"
%{G%}:н <=> %{I%}: %>: _ %{A%}: ;

"'н' addition between {I} and {D} in locative" 
0:н <=> %{I%}: %>: _ %{D%}: %{A%}: [ \н | .#. ] ;

"B -> п in negative"
%{B%}:п <=> UnvoicedNonSonCns: %>: _  ;

!иште<v><iv><inf>:иште>{U}{U}
!                 иштө>{U}{U}

! ndt	N
! ldt	L
! gk	G
! dt	D
! mbp	M

